#
# A single line theme with Git information and shortened path to PWD
#
# Authors:
#   Tronje <hi@tron.je>
#   Kyle West <kswest@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Features:
#   - Indicates dirty Git repository.
#   - Indicates vi modes.
#   - Abbreviated path
#

function prompt_jlk_pwd {
  local pwd="${PWD/#$HOME/~}"
  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_jlk_pwd="$MATCH"
  else
    _prompt_jlk_pwd="${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}/${pwd:t}"
  fi
}

function prompt_jlk_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # call prompt pwd formatting function
  prompt_jlk_pwd
}

function prompt_jlk_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_jlk_precmd

  # Set editor-info parameters.
  # this defines what the prompt-thingy will look like
  zstyle ':prezto:module:editor:info:completing' format '...'
  zstyle ':prezto:module:editor:info:keymap:primary' format '->'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '-<'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'no'
  zstyle ':prezto:module:git:info:branch' format '%F{yellow}%b%f'
  zstyle ':prezto:module:git:info:dirty' format '%B%F{red}!%f%b'
  zstyle ':prezto:module:git:info:indexed' format ' '
  zstyle ':prezto:module:git:info:unindexed' format ' '
  zstyle ':prezto:module:git:info:untracked' format ' '
  zstyle ':prezto:module:git:info:keys' format 'prompt' '- %b%D '

  # Define prompts.
  PROMPT='%F{cyan}${_prompt_jlk_pwd}%f ${git_info:+${(e)git_info[prompt]}}${editor_info[keymap]} '
}

prompt_jlk_setup "$@"
